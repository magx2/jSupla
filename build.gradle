plugins {
	id "com.jfrog.bintray" version "1.7.3"
}

ext {
	slf4j_version = '1.7.25'
	random_beans_version = '3.7.0'
	reactorVersion = "3.1.1.RELEASE"
}

@SuppressWarnings("UnnecessaryQualifiedReference")
def prodProjects(Closure c) {
	allprojects { p ->
		if (!isTestProject(p)) { // only for non test projects
			c.delegate = p
			c.resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST
			c(p)
		}
	}
}

private static boolean isTestProject(Project p) {
	p.name.toLowerCase().contains("test")
}

@SuppressWarnings("UnnecessaryQualifiedReference")
def releasableProjects(Closure c) {
	allprojects { p ->
		if (isReleasableProject(p)) {
			c.delegate = p
			c.resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST
			c(p)
		}
	}
}

private static boolean isReleasableProject(Project p) {
	true
}

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

allprojects {
	apply plugin: 'java'
	group = 'pl.grzeslowski.jSupla'
	version = '0.1.0-SNAPSHOT'
	sourceCompatibility = 1.8
}

// Add check style to all projects
prodProjects {
	apply plugin: 'checkstyle'
	tasks.withType(Checkstyle) {
		configFile = "${rootDir.path}/config/checkstyle/checkstyle.xml" as File
	}
}

// Add JaCoCO to all projects
prodProjects {
	apply plugin: "jacoco"
	tasks["build"].dependsOn "jacocoTestReport"
}

allprojects {
	dependencies {
		compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version

		testCompile 'junit:junit:4.12'
		testCompile 'org.assertj:assertj-core:3.8.0'
		testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
		testCompile group: 'io.github.benas', name: 'random-beans', version: random_beans_version
		testCompile group: 'io.github.benas', name: 'random-beans-validation', version: random_beans_version
		testCompile group: 'org.slf4j', name: 'slf4j-simple', version: slf4j_version
		testCompile group: 'com.google.guava', name: 'guava', version: '23.3-jre'
		testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '2.3.3'
	}
}

releasableProjects { Project project ->
	ext {
		bintrayConfig = [
				repo           : "bigboy",
				userOrg        : "big-boy",
				websiteUrl     : "https://github.com/magx2/jSupla",
				issueTrackerUrl: "https://github.com/magx2/jSupla/issues",
				vcsUrl         : "https://github.com/magx2/jSupla.git",
				licenses       : ['MIT'],
				labels         : ['Supla', "ioT"],
				githubRepo     : "magx2/jSupla",
		]
	}
	apply from: "https://raw.githubusercontent.com/magx2/magx2.github.io/bintray.gradle-1.1.0/commons/gradle/bintray.gradle"
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.2.1'
}