plugins {
	id 'maven-publish'
	id 'signing'
}

ext {
	slf4j_version = '2.0.11'
	random_beans_version = '3.9.0'
	reactorVersion = "3.6.2"
}

@SuppressWarnings("UnnecessaryQualifiedReference")
def allProjects(Closure c) {
	allprojects { p ->
		if (!isRootProject(p)) { // only for non test projects
			c.delegate = p
			c.resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST
			c(p)
		}
	}
}

private static boolean isRootProject(Project p) {
	p == p.getRootProject()
}

@SuppressWarnings("UnnecessaryQualifiedReference")
def prodProjects(Closure c) {
	allprojects { p ->
		if (!isTestProject(p) && !isRootProject(p)) { // only for non test projects
			c.delegate = p
			c.resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST
			c(p)
		}
	}
}

private static boolean isTestProject(Project p) {
	p.name.toLowerCase().contains("test")
}

@SuppressWarnings("UnnecessaryQualifiedReference")
def releasableProjects(Closure c) {
	allprojects { p ->
		if (isReleasableProject(p)) {
			c.delegate = p
			c.resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST
			c(p)
		}
	}
}

private static boolean isReleasableProject(Project p) {
	!isRootProject(p)
}

allprojects {
	repositories {
		mavenCentral()
	}
}

allProjects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	group = 'pl.grzeslowski.jSupla'
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
}

allProjects {
	version jSuplaVersion
	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}
	}
}

// Integration tests
allProjects {
	apply plugin: 'idea'
	sourceSets {
		integrationTest {
			java {
				compileClasspath += main.output + test.output
				runtimeClasspath += main.output + test.output
				srcDir file('src/integrationTest/java')
			}
			resources.srcDir file('src/integrationTest/resources')
		}
	}
	configurations {
		integrationTestCompile.extendsFrom testCompile
		integrationTestImplementation.extendsFrom testImplementation
		integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	}
	task integrationTest(type: Test) {
		description = 'Runs integration tests.'
		group = 'verification'

		testClassesDirs = sourceSets.integrationTest.output.classesDirs
		classpath = sourceSets.integrationTest.runtimeClasspath
		shouldRunAfter test
		outputs.upToDateWhen { false }
	}
	check.dependsOn integrationTest

	// Allow running integration tests in IntelliJ
	idea {
		module {
			sourceSets.integrationTest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
		}
	}
}

// lombok
allProjects {
	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.32'
		annotationProcessor 'org.projectlombok:lombok:1.18.32'

		testCompileOnly 'org.projectlombok:lombok:1.18.32'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
	}

	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

// Maven Central release
prodProjects {
	task javadocJar(type: Jar) {
		archiveClassifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		archiveClassifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

//	signing {
//		sign configurations.archives
//	}

//	tasks.withType(Sign)*.enabled = !version.endsWith("SNAPSHOT")

	group = project.group
	archivesBaseName = project.name
	version = project.version

	def final githubRepo = "magx2/jSupla"
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java

				pom {
					name.set(project.name)
					description.set(project.description)
//					packaging = 'jar'
					url.set("https://github.com/${githubRepo}")

					scm {
						connection.set("scm:git:git://github.com/${githubRepo}.git")
						developerConnection.set("scm:git:git://github.com/${githubRepo}.git")
						url.set("https://github.com/${githubRepo}.git")
					}

					licenses {
						license {
							name.set("MIT")
							url.set("https://opensource.org/licenses/MIT")
						}
					}

					developers {
						developer {
							id.set('magx2')
							name.set('Martin Grzeslowski')
							email.set('hello@grzeslowski.pl')
						}
					}
				}
			}
		}

		repositories {
			maven {
				name = "sonatype"
				url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
			}

			maven {
				name = "sonatypeSnapshots"
				url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
			}
		}
	}

	signing {
		sign(publishing.publications["mavenJava"])
	}
}

//tasks.named('signArchives').configure {
tasks.named('signMavenJavaPublication').configure {
	onlyIf {
		!project.hasProperty('skipSigning') || !project.skipSigning.toBoolean()
	}
}

allProjects {
	dependencies {
		implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version

		testImplementation 'junit:junit:4.12'
		testImplementation 'org.assertj:assertj-core:3.8.0'
		testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
		testImplementation group: 'io.github.benas', name: 'random-beans', version: random_beans_version
		testImplementation group: 'io.github.benas', name: 'random-beans-validation', version: random_beans_version
		testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4j_version
		testImplementation group: 'com.google.guava', name: 'guava', version: '23.3-jre'
		testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.3'
	}
}

apply from: 'release.gradle'