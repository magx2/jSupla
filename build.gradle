ext {
	slf4j_version = '1.7.25'
}

repositories {
	mavenLocal()
	mavenCentral()
}

allprojects {
	apply plugin: 'java'
	group = 'pl.grzeslowski.jSupla'
	version = '0.1.0-SNAPSHOT'
	sourceCompatibility = 1.8
}

// Add check style to all projects
allprojects { p ->
	if(!isTestProject(p)) { // only for non test projects
		apply plugin: 'checkstyle'
		p.tasks.withType(Checkstyle) {
			configFile = "${rootDir.path}/config/checkstyle/checkstyle.xml" as File
		}
	}
}

// Add JaCoCO to all projects
allprojects { p ->
	if(!isTestProject(p)) { // only for non test projects
		apply plugin: "jacoco"
	}
}

allprojects {
	dependencies {
		compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version

		testCompile 'junit:junit:4.12'
		testCompile 'org.assertj:assertj-core:3.8.0'
	}
}

private static boolean isTestProject(Project p) {
	p.name.toLowerCase().contains("test")
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.0.1'
}