import groovy.json.JsonSlurper

import java.nio.file.Files
import java.nio.file.Path
import java.time.ZonedDateTime
import java.util.stream.Collectors
import java.util.stream.Stream

void generatePojo(struct, Path generatedSourceRoot) {
	def javaName = struct.overrideName ?: struct.name
	def path = "api/structs"
	def sourcesPath = project.file("src/main/java/pl/grzeslowski/jsupla/protocol/").toPath().resolve(path).resolve(javaName + ".java")
	if (Files.exists(sourcesPath)) {
		logger.warn("Struct {} already exists, ignoring...", sourcesPath.toFile().path)
		return
	}

	def prefix = struct.prefix
	if (prefix != "T") {
		path += "/" + prefix.toLowerCase().substring(1)
	}
	def imports = new TreeSet<>([
		"import static pl.grzeslowski.jsupla.protocol.api.Preconditions.*",
		"import static pl.grzeslowski.jsupla.protocol.api.JavaConsts.*",
		"import static pl.grzeslowski.jsupla.protocol.api.consts.ProtoConsts.*",
		"import pl.grzeslowski.jsupla.protocol.api.types.ProtoWithSize",
		"import pl.grzeslowski.jsupla.protocol.api.structs.*",
	])
	def javaDoc = buildJavaDoc(struct)
	def constructorAssignments = buildConstructorAssignments(struct)
	def properties = buildProperties(struct)
	def implementClass = findImplementClass(struct)
	def traitInterface = ""
	if (struct.traitInterface) {
		traitInterface += ", " + struct.traitInterface
	}
	def callType = findCallType(struct)
	def hasDynamicSize = hasDynamicSize(struct)
	def size = findSizeForClass(struct)
	def staticSize = ""
	if (!hasDynamicSize) {
		staticSize = "public static final int SIZE = ${size}\n" +
			"\t\t;"
		size = "SIZE"
	}
	def toString = ""
	if (shouldOverrideToString(struct)) {
		def fields = struct.fields
			.stream()
			.map { field ->
				def value =field.name
				if(overrideString(field)) {
					if (field.type    == "int"
						|| field.type == "integer"
						|| field.type == "char"
						|| field.type == "byte"
						|| field.type == "short"
						|| field.type == "long") {
						value = "java.util.Arrays.toString($value)"
					} else {
						value = 'java.util.Arrays.stream(' +value+ ').map(Object::toString).collect(java.util.stream.Collectors.joining(", ", "[", "]"))'
					}
				}
				
				"${field.name}=\"+$value"
			}
			.collect(Collectors.joining("+\", ", "", "+\""))
		toString = """\
			|@Override public String toString() {
			|  return "${struct.name}[${fields}]";
			|}
			""".stripMargin()
	}
	def content = """\
		|package pl.grzeslowski.jsupla.protocol.${path.replaceAll("/", ".")};
		|
		|${String.join(";\n|", imports)};
		|
		|$javaDoc
		|@javax.annotation.Generated(value="Struct original name: ${struct.rawName}", date = "${ZonedDateTime.now()}")
		|public record ${javaName}($properties) implements ${implementClass}$traitInterface {
		|	${staticSize}
		|	
		|	public ${javaName} {
		|		${constructorAssignments}
		|	}
		|	$callType	
		|
		|	@Override
		|	public int protoSize() {
		|		return ${size}
		|			;
		|	}
		|
		|	$toString
		|}
		|""".stripMargin()


	def toWrite = generatedSourceRoot.resolve(path).resolve(javaName + ".java")
	Files.createDirectories(toWrite.parent)
	toWrite.write(content)
}

task pojoGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "proto.h.json")
	outputs.dir project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "proto.h.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def structs = jsonSlurper.parse(jsonFile) as List

		def generatedSourceRoot = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol").toPath()
		Files.createDirectories(generatedSourceRoot)

		structs
			.stream()
			.sorted { a, b -> a.rawName.compareTo(b.rawName) }
			.forEach { generatePojo(it, generatedSourceRoot) }
	}
}
pojoGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "pojoGenerator"
sourcesJar.dependsOn "pojoGenerator"


void generateTrait(traitInterface, Path generatedSourceRoot) {
	def javaName = traitInterface.name
	def path = "api/structs"
	def sourcesPath = project.file("src/main/java/pl/grzeslowski/jsupla/protocol/").toPath().resolve(path).resolve(javaName + ".java")
	if (Files.exists(sourcesPath)) {
		logger.warn("Struct {} already exists, ignoring...", sourcesPath.toFile().path)
		return
	}
	def prefix = traitInterface.structs[0].prefix
	if (prefix != "T") {
		path += "/" + prefix.toLowerCase().substring(1)
	}
	def permitClasses = traitInterface.structs
		.stream()
		.map { it.overrideName ?: it.name }
		.collect(Collectors.joining(", "))
	def javaDoc = traitInterface.structs
		.stream()
		.map { it.overrideName ?: it.name }
		.map { "<li>{@link $it}</li>" }
		.collect(Collectors.joining("\n * \t", "<ul>\n * \t", "\n * </ul>"))
	def content = """\
		|package pl.grzeslowski.jsupla.protocol.${path.replaceAll("/", ".")};
		|
		|/**
		| * Allowed classes:
		| * $javaDoc
		| */
		|@javax.annotation.Generated(value="Mark up trait", date = "${ZonedDateTime.now()}")
		|public sealed interface ${javaName} permits $permitClasses {
		|}
		|""".stripMargin()
	def toWrite = generatedSourceRoot.resolve(path).resolve(javaName + ".java")
	Files.createDirectories(toWrite.parent)
	toWrite.write(content)
}

task traitGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "proto.h.json")
	outputs.dir project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "traits.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def traits = jsonSlurper.parse(jsonFile) as List

		def generatedSourceRoot = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol").toPath()
		Files.createDirectories(generatedSourceRoot)

		traits
			.stream()
			.sorted { a, b -> a.name.compareTo(b.name) }
			.forEach { generateTrait(it, generatedSourceRoot) }
	}
}
traitGenerator.dependsOn "parseProtoh"
pojoGenerator.dependsOn "traitGenerator"
pojoGenerator.dependsOn "traitGenerator"

static String buildJavaDoc(struct) {
	def javaDoc = "/**\n"
	if (struct.javaDoc) {
		javaDoc += Arrays.stream(struct.javaDoc.toString().split("\n"))
			.map {
				it.replaceAll("&", "&amp;")
					.replaceAll("<", "&#60;")
					.replaceAll(">", "&#62;")
					.replaceAll("@", "&#64;")
			}
			.map { " * $it" }
			.collect(Collectors.joining("\n")) + "\n * <p>\n"
	}
	if (struct.raw) {
		javaDoc += " * Original code:\n * <pre>\n * "
		javaDoc += struct.raw.toString()
			.replaceAll("&", "&amp;")
			.replaceAll("<", "&#60;")
			.replaceAll(">", "&#62;")
			.replaceAll("@", "&#64;")
			.replaceAll('\n', '\n * ')
		javaDoc += "\n * </pre>"
	}
	// parameters
	javaDoc += struct.fields
		.stream()
		.flatMap { field ->
			if (field.originalClassName == "SimpleField") {
				Stream.of(field)
			} else if (field.originalClassName == "UnionField") {
				field.fields.stream()
			} else {
				throw new IllegalArgumentException("Unknown type: " + field.originalClassName.toString())
			}
		}
		.map { field ->
			def comment = (field.javaDoc ?: "")
				.replaceAll("&", "&amp;")
				.replaceAll("<", "&#60;")
				.replaceAll(">", "&#62;")
				.replaceAll("@", "&#64;")
			if (field.unsigned) {
				if (field.javaDoc) {
					comment += ". "
				}
				comment += 'unsigned {@code ' + field.type + '}'
			}
			"@param ${field.name} $comment"
		}
		.collect(Collectors.joining("\n * ", "\n * ", "\n"))
	return javaDoc + "*/"
}

static String buildConstructorAssignments(struct) {
	struct.fields
		.stream()
		.flatMap { field ->
			if (field.originalClassName == "SimpleField") {
				return Stream.of(buildConstructorAssignment(field))
			} else if (field.originalClassName == "UnionField") {
				def assignments = field.fields.stream().map { buildConstructorAssignment(it) }
				def union = field.fields
					.stream()
					.map { it.name }
					.collect(Collectors.joining(", "))
				//noinspection GroovyAssignabilityCheck
				return Stream.concat(
					assignments,
					Stream.of("unionCheck($union);")
				)
			}
		}
		.filter { it != null }
		.collect(Collectors.joining("\n\t\t|\t\t"))
}

static String buildConstructorAssignment(field) {
	def empty = true
	def value = field.name
	if (field.array) {
		def size = "(int) ${field.arraySize}"
		value = "checkArrayLength(${value}, $size)"
		empty = false
	}
	if (field.unsigned) {
		value = "unsigned(${value})"
		empty = false
	}
	if (field.extraCheck) {
		value = String.format(field.extraCheck, value)
		empty = false
	}
	empty ? null : "$value;"
}

static String mapSuplaTypeToJava(field) {
	def array = field.array ? "[]" : ""
	def lowerType = field.type.toLowerCase()
	if (field.unsigned) {
		if (lowerType == "int" || lowerType == "integer") {
			return matchType("long", field.type, array)
		}
		if (lowerType == "char" || lowerType == "byte") {
			return matchType("short", field.type, array)
		}
		if (lowerType == "short") {
			return matchType("int", field.type, array)
		}
		if (lowerType == "long") {
			return matchType("java.math.BigInteger", field.type, array)
		}
	} else {
		if (lowerType == "int" || lowerType == "integer") {
			return matchType("int", field.type, array)
		}
		if (lowerType == "short") {
			return matchType("short", field.type, array)
		}
		if (lowerType == "long") {
			return matchType("long", field.type, array)
		}
		if (lowerType == "char" || lowerType == "byte") {
			return matchType("byte", field.type, array)
		}
	}
	return findImportClass(field) + field.type + array
}

static String findImportClass(field) {
	if (field.rawPrefix == "T") {
		return "pl.grzeslowski.jsupla.protocol.api.structs."
	}

	def prefix = field.rawPrefix.toString().substring(1).toLowerCase()
	return "pl.grzeslowski.jsupla.protocol.api.structs.${prefix}."
}

static String buildProperties(struct) {
	struct.fields
		.stream()
		.flatMap { field ->
			if (field.originalClassName == "SimpleField") {
				Stream.of(buildSimpleProperty(field))
			} else if (field.originalClassName == "UnionField") {
				field.fields.stream().map { buildSimpleProperty(it) }
			} else {
				throw new IllegalArgumentException("Unknown type: " + field.originalClassName.toString())
			}
		}
		.collect(Collectors.joining(",\n\t|\t"))
}

static String buildSimpleProperty(field) {
	def type = mapSuplaTypeToJava(field)
	"${type} ${field.name}"
}

static String findImplementClass(struct) {
	def prefix = struct.prefix
	if (prefix == "T") {
		return "pl.grzeslowski.jsupla.protocol.api.types.ProtoWithSize"
	}
	prefix = prefix.toString().substring(1).toLowerCase()

	def implementClass = "pl.grzeslowski.jsupla.protocol.api.structs.${prefix}."
	for (def letter : prefix) {
		def s
		if (letter == "c") {
			implementClass += "Client"
		} else if (letter == "s") {
			implementClass += "Server"
		} else if (letter == "d") {
			implementClass += "Device"
		} else {
			throw new IllegalArgumentException("[structs] Unknown letter [$letter] !")
		}
	}
	return implementClass
}

@SuppressWarnings('GrMethodMayBeStatic')
String findCallType(struct) {
	def clazz = findImplementClass(struct)
	if (clazz == "pl.grzeslowski.jsupla.protocol.api.types.ProtoWithSize") {
		return "/* no call type */"
	}

	def returnValue = "pl.grzeslowski.jsupla.protocol.api.calltypes." + clazz.split("\\.")[-1]


	def any = ext.callTypes
		.stream()
		.filter { it.proto == struct.rawName }
		.map { it.callType }
		.map { "return ${returnValue}CallType.$it;" }
		.findAny()
	if (!any.isPresent() && !ext.noCallTypes.contains(struct.rawName)) {
		throw new IllegalArgumentException("Cannot find call type for: " + struct.rawName)
	}
	def callType = any.orElseGet {
		"throw new UnsupportedOperationException(\"CallType for \" +getClass().getSimpleName()+\" is not set!\");"
	}

	return """
			|	@Override
    		|	public ${returnValue}CallType callType() {
        	|		$callType
    		|	}""".stripMargin()
}

static String findSizeForClass(struct) {
	if (struct.fields.isEmpty()) {
		return "0"
	}
	struct.fields
		.stream()
		.map { field ->
			if (field.originalClassName == "SimpleField") {
				return findSizeForField(field)
			} else if (field.originalClassName == "UnionField") {
				def sizes = field.fields
					.stream()
					.map { findSizeForUnionField(it) }
					.collect(Collectors.joining("\n\t\t\t,"))
				return "pl.grzeslowski.jsupla.protocol.api.JavaConsts.unionSize(\n\t\t\t$sizes\n\t\t)"
			} else {
				throw new IllegalArgumentException("Unknown type: " + field.originalClassName.toString())
			}
		}
		.collect(Collectors.joining(" + "))
}

boolean hasDynamicSize(struct) {
	return ext.dynamicSize.contains(struct.name)
}

static String findSizeForField(field) {
	findSize(field, false)
}

// might be a problem cause not set union fields still have size...
// probably should introduce MIN_SIZE
static String findSizeForUnionField(field) {
	"${findSize(field, true)}"
}

static String findSize(field, union) {
	def type = field.type.toLowerCase()
	if (field.array) {
		def xSize = mapSimpleType(type)
		if (xSize.startsWith("UNKNOWN")) {
			return "arraySize(" + field.name + ")"
		} else {
			if (field.arraySize) {
				return "(int) ${field.arraySize} * ${xSize}"
			} else {
				return "${field.name}.length * ${xSize}"
			}
		}
	} else if (field.unsigned) {
		if (type == "byte") {
			return "CHAR_SIZE"
		} else if (type == "short") {
			return "SHORT_SIZE"
		} else if (type == "int" || type == "integer") {
			return "INT_SIZE"
		} else if (type == "char") {
			return "CHAR_SIZE"
		} else if (field.type == "long") {
			return "LONG_SIZE"
		} else {
			throw new IllegalArgumentException("Unknown unsigned type: " + type)
		}
	} else {
		def finalType = mapSimpleType(mapSuplaTypeToJava(field))
		if (finalType.startsWith("UNKNOWN")) {
			if (union) {
				finalType = "(${field.name}!= null ? ${field.name}.protoSize() : ${mapSuplaTypeToJava(field)}.SIZE)"
			} else {
				finalType = field.name + ".protoSize()"
			}
		}
		return finalType
	}
}

static String mapSimpleType(String type) {
	type = type.toLowerCase()
	if (type == "int" || type == "integer") {
		return "INT_SIZE"
	} else if (type == "char" || type == "byte") {
		return "BYTE_SIZE"
	} else if (type == "short") {
		return "SHORT_SIZE"
	} else if (type == "long") {
		return "LONG_SIZE"
	}
	return "UNKNOWN_" + type
}

static String matchType(String newType, String originalType, String array) {
	if (originalType == originalType.toLowerCase()) {
		return newType + array
	}
	if (newType == "int" && originalType == "Short") {
		return "Integer" + array
	}
	if (newType == "int" && originalType == "Int") {
		return "Integer" + array
	}
	if (array) {
		return newType + array
	} else {
		return newType.capitalize() + array
	}
}

static boolean shouldOverrideToString(struct) {
	return struct.fields
		.stream()
		.anyMatch { field -> overrideString(field) }
}

static boolean overrideString(field) {
	return field.array 
}
