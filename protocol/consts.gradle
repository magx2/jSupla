import groovy.json.JsonSlurper

import java.nio.file.Files
import java.nio.file.Path
import java.time.ZonedDateTime
import java.util.stream.Collectors
import java.util.stream.Stream

List overrides = [
	[name: "SUPLA_MAX_DATA_SIZE", value: "3264"],
	[name: "SPROTO_WITHOUT_OUT_BUFFER", value: "true"],
	[name: "SUPLA_CHANNELMAXCOUNT", value: "128"],
	[name: "SUPLA_CHANNELEXTENDEDVALUE_SIZE", value: "1024"],
	[name: "EM_MEASUREMENT_COUNT", value: "5"],
	[name: "PROTO_ICACHE_FLASH", value: "true"],
]

task constsGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.file project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/consts/ProtoConsts.java")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def consts = (jsonSlurper.parse(jsonFile) as List)
			.stream()
			.filter { !predefined(it, overrides) }
			.toList()

		def protoConsts = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/consts/ProtoConsts.java").toPath()
		Files.createDirectories(protoConsts.parent)

		def java = """\
			|package pl.grzeslowski.jsupla.protocol.api.consts;
			|
			|@javax.annotation.Generated(value="Const from proto.h", date = "${ZonedDateTime.now()}", comments = "generated from ProtoConsts.h")
			|public interface ProtoConsts {
			|	public static final byte[] SUPLA_TAG = new byte[]{83, 85, 80, 76, 65};
			|""".stripMargin()
		java += Stream.concat(consts.stream(), overrides.stream())
			.filter { !it.name.startsWith("_") }
			.filter { it.name != "timeval" }
			.filter { it.name != "SUPLA_CHANNELEXTENDEDVALUE_PACK_MAXDATASIZE" }
			.filter { !isCallType(it.name) }
			.filter { !isResultCode(it.name) }
			.filter { !isChannelType(it.name) }
			.filter { !isChannelFlag(it.name) }
			.filter { !isChannelFunction(it.name) }
			.map { generateConst(it, consts) }
			.collect(Collectors.joining("\n\t"))
		java += """
			|	public static final int SUPLA_CHANNELEXTENDEDVALUE_PACK_MAXDATASIZE = SUPLA_MAX_DATA_SIZE - 50;
			|}""".stripMargin()

		protoConsts.write(java)
	}
}
constsGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "constsGenerator"

task callTypesGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.dir project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/calltypes/")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def consts = (jsonSlurper.parse(jsonFile) as List)
			.stream()
			.filter { isCallType(it.name) }
			.toList()

		def callTypesFolder = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/calltypes/").toPath()
		Files.createDirectories(callTypesFolder)

		buildCallType('ClientServer', "SUPLA_CS_", callTypesFolder, consts)
		buildCallType('ClientServerDevice', "SUPLA_CSD_", callTypesFolder, consts)
		buildCallType('DeviceClientServer', "SUPLA_DCS_", callTypesFolder, consts)
		buildCallType('DeviceServer', "SUPLA_DS_", callTypesFolder, consts)
		buildCallType('DeviceServerClient', "SUPLA_DSC_", callTypesFolder, consts)
		buildCallType('ServerClient', "SUPLA_SC_", callTypesFolder, consts)
		buildCallType('ServerClientDevice', "SUPLA_SCD_", callTypesFolder, consts)
		buildCallType('ServerClientServer', "SUPLA_SCS_", callTypesFolder, consts)
		buildCallType('ServerDevice', "SUPLA_SD_", callTypesFolder, consts)
		buildCallType('ServerDeviceClient', "SUPLA_SDC_", callTypesFolder, consts)
		buildCallType('ServerDeviceServer', "SUPLA_SDS_", callTypesFolder, consts)
	}
}
callTypesGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "callTypesGenerator"

task resultCodeGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.file project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ResultCode.java")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def resultCodes = (jsonSlurper.parse(jsonFile) as List)
			.stream()
			.filter { isResultCode(it.name) }
			.map {
				def javaDoc = ""
				if (it.javaDoc) {
					javaDoc = "/** ${it.javaDoc} */\n\t"
						.replaceAll("&", "&amp;")
						.replaceAll("&", "&amp;")
						.replaceAll("<", "&#60;")
						.replaceAll(">", "&#62;")
				}
				"$javaDoc${it.name}(${it.value})"
			}
			.collect(Collectors.joining(",\n\t"))

		def resultCodePath = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ResultCode.java").toPath()
		Files.createDirectories(resultCodePath.parent)

		resultCodePath.write("""\
			|package pl.grzeslowski.jsupla.protocol.api;
			|
			|@javax.annotation.Generated(value="Const from proto.h", date = "${ZonedDateTime.now()}", comments = "generated from ProtoConsts.h")
			|@lombok.Getter @lombok.RequiredArgsConstructor
			|public enum ResultCode {
			|	$resultCodes
			|	;
			|	private final int value;
			|}""".stripMargin())
	}
}
resultCodeGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "resultCodeGenerator"

task channelTypeGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.file project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelType.java")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def channelTypes = Stream.concat(
			(jsonSlurper.parse(jsonFile) as List).stream(),
			Stream.of([name: "UNKNOWN", value: "-1", javaDoc: "This is when you don't know ChannelType, i.e. when smth was added to cloud/device code without changing it here."]))
			.filter { isChannelType(it.name) }
			.map {
				def javaDoc = ""
				if (it.javaDoc) {
					javaDoc = "/** ${it.javaDoc} */\n\t"
						.replaceAll("&", "&amp;")
						.replaceAll("<", "&#60;")
						.replaceAll(">", "&#62;")
						.replaceAll("@", "&#64;")
				}
				"$javaDoc${it.name}(${it.value})"
			}
			.collect(Collectors.joining(",\n\t"))

		def channelTypePath = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelType.java").toPath()
		Files.createDirectories(channelTypePath.parent)

		channelTypePath.write("""\
			|package pl.grzeslowski.jsupla.protocol.api;
			|
			|import lombok.val;
			|import java.util.Arrays;
			|import java.util.Optional;
			|
			|@javax.annotation.Generated(value="Const from proto.h", date = "${ZonedDateTime.now()}", comments = "generated from ProtoConsts.h")
			|@lombok.Getter @lombok.RequiredArgsConstructor @lombok.extern.slf4j.Slf4j
			|public enum ChannelType {
			|	$channelTypes
			|	;
			|	private final int value;
			|
    		|	public static Optional<ChannelType> findByValue(int value) {
    		|	    val any = Arrays.stream(values())
    		|	        .filter(type -> type.value == value)
    		|	        .findAny();
    		|	    if (!any.isPresent()) {
    		|	        log.warn("Unknown channel type value: {}", value);
    		|	    }
    		|	    return any;
    		|	}
			|}""".stripMargin())
	}
}
channelTypeGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "channelTypeGenerator"

task channelFlagGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.file project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelFlag.java")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def channelFlags = (jsonSlurper.parse(jsonFile) as List).stream()
			.filter { isChannelFlag(it.name) }
			.map {
				def javaDoc = ""
				if (it.javaDoc) {
					javaDoc = "/** ${it.javaDoc} */\n\t"
						.replaceAll("&", "&amp;")
						.replaceAll("<", "&#60;")
						.replaceAll(">", "&#62;")
						.replaceAll("@", "&#64;")
				}
				def deprecated = it?.javaDoc?.contains("DEPRECATED") ? "\n\t@Deprecated" : ""
				"$javaDoc$deprecated ${it.name}(${it.value})"
			}
			.collect(Collectors.joining(",\n\t"))

		def channelTypePath = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelFlag.java").toPath()
		Files.createDirectories(channelTypePath.parent)

		channelTypePath.write("""\
			|package pl.grzeslowski.jsupla.protocol.api;
			|
			|import lombok.val;
			|import java.util.Arrays;
			|import java.util.Optional;
			|import java.util.EnumSet;
			|import java.util.stream.Collectors;
			|
			|@javax.annotation.Generated(value="Const from proto.h", date = "${ZonedDateTime.now()}", comments = "generated from ProtoConsts.h")
			|@lombok.Getter @lombok.RequiredArgsConstructor @lombok.extern.slf4j.Slf4j
			|public enum ChannelFlag {
			|	$channelFlags
			|	;
			|	private final int value;
			|
			|   public static java.util.Set<ChannelFlag> findByValue(int value) {
			|   	return Arrays.stream(ChannelFlag.values())
			|   			.filter(flag -> (value & flag.value) != 0)
			|   			.collect(Collectors.toCollection(() -> EnumSet.noneOf(ChannelFlag.class)));
			|   }
			|}""".stripMargin())
	}
}
channelFlagGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "channelFlagGenerator"

static boolean isCallType(String name) {
	return name.startsWith("SUPLA_CS_") ||
		name.startsWith("SUPLA_CSD_") ||
		name.startsWith("SUPLA_DCS_") ||
		name.startsWith("SUPLA_DS_") ||
		name.startsWith("SUPLA_DSC_") ||
		name.startsWith("SUPLA_SC_") ||
		name.startsWith("SUPLA_SCD_") ||
		name.startsWith("SUPLA_SCS_") ||
		name.startsWith("SUPLA_SD_") ||
		name.startsWith("SUPLA_SDC_") ||
		name.startsWith("SUPLA_SDS_")
}

task channelFunctionGenerator {
	group = 'jSupla'
	inputs.file new File(project.file("build/parser"), "consts.json")
	outputs.file project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelFunction.java")
	doLast {
		def jsonFile = new File(project.file("build/parser"), "consts.json")
		if (!jsonFile.exists()) {
			throw new IOException("File ${jsonFile.absolutePath} does not exists!")
		}

		def jsonSlurper = new JsonSlurper()
		def channelFunctions = (jsonSlurper.parse(jsonFile) as List)
			.stream()
			.filter { isChannelFunction(it.name) }
			.map {
				def javaDoc = ""
				if (it.javaDoc) {
					javaDoc = "/** ${it.javaDoc} */\n\t"
						.replaceAll("&", "&amp;")
						.replaceAll("<", "&#60;")
						.replaceAll(">", "&#62;")
						.replaceAll("@", "&#64;")
				}
				"$javaDoc${it.name}(${it.value})"
			}
			.collect(Collectors.joining(",\n\t"))

		def channelTypePath = project.file("build/generated/sources/jsupla/main/java/pl/grzeslowski/jsupla/protocol/api/ChannelFunction.java").toPath()
		Files.createDirectories(channelTypePath.parent)

		channelTypePath.write("""\
			|package pl.grzeslowski.jsupla.protocol.api;
			|
			|import lombok.val;
			|import java.util.Arrays;
			|import java.util.Optional;
			|
			|@javax.annotation.Generated(value="Const from proto.h", date = "${ZonedDateTime.now()}", comments = "generated from ProtoConsts.h")
			|@lombok.Getter @lombok.RequiredArgsConstructor @lombok.extern.slf4j.Slf4j
			|public enum ChannelFunction {
			|	$channelFunctions
			|	;
			|	private final int value;
			|}""".stripMargin())
	}
}
channelFunctionGenerator.dependsOn "parseProtoh"
compileJava.dependsOn "channelFunctionGenerator"

static boolean isResultCode(String name) {
	if (name == "SUPLA_RESULTCODE_DENY_CHANNEL_IS_ASSOCIETED_WITH_ACTION_TRIGGER") {
		return false
	}
	name.startsWith("SUPLA_RESULTCODE_")
}

static boolean isChannelType(String name) {
	if (name == "UNKNOWN") {
		return true
	}
	return name.startsWith("SUPLA_CHANNELTYPE_") ||
		name.startsWith("EV_TYPE") ||
		name.startsWith("SUBV_TYPE_") ||
		name.startsWith("CALCFG_TYPE_")
}

static boolean isChannelFlag(String name) {
	return name.startsWith("SUPLA_CHANNEL_FLAG_") 
}

static boolean isChannelFunction(String name) {
	return name.startsWith("SUPLA_CHANNELFNC_")
}

static void buildCallType(String callTypeName, String callTypePrefix, Path callTypesFolder, consts) {
	def className = callTypeName + "CallType"
	def fileName = className + ".java"
	def fields = consts.stream()
		.filter { it.name.startsWith(callTypePrefix) }
		.map {
			def javaDoc = ""
			if (it.javaDoc) {
				javaDoc = "/** ${it.javaDoc} */\n\t"
					.replaceAll("&", "&amp;")
					.replaceAll("<", "&#60;")
					.replaceAll(">", "&#62;")
					.replaceAll("@", "&#64;")
			}
			"$javaDoc${it.name}(${it.value})"
		}
		.collect(Collectors.joining(",\n\t"))
	def content = """\
		|package pl.grzeslowski.jsupla.protocol.api.calltypes;
		|
		|@lombok.Getter
		|@lombok.RequiredArgsConstructor
		|@javax.annotation.Generated(value="Prefix '$callTypePrefix'", date = "${ZonedDateTime.now()}")
		|public enum ${className} implements CallType {
		|	$fields;
		|	private final int value;
		|}""".stripMargin()

	callTypesFolder.resolve(fileName).write(content)
}

static String generateConst(constObj, List consts) {
	def value = consts
		.stream()
		.filter { it.name == constObj.value }
		.findAny()
	if (value.isPresent()) {
		def javaDoc = ""
		if (value.get().javaDoc) {
			javaDoc = "/** ${value.get().javaDoc} */\n\t"
				.replaceAll("&", "&amp;")
				.replaceAll("<", "&#60;")
				.replaceAll(">", "&#62;")
				.replaceAll("@", "&#64;")
		}
		def type = findType(value.get().value)
		return "${javaDoc}public static final $type ${constObj.name} = ${value.get().name};"
	}

	def javaDoc = ""
	if (constObj.javaDoc) {
		javaDoc = "/** ${constObj.javaDoc} */\n\t"
			.replaceAll("&", "&amp;")
			.replaceAll("<", "&#60;")
			.replaceAll(">", "&#62;")
			.replaceAll("@", "&#64;")
	}
	def type = findType(constObj.value)
	return "${javaDoc}public static final $type ${constObj.name} = ${mapValue(constObj.value)};"
}

static String mapValue(String value) {
	if (value == null) {
		return "true"
	}
	if (value == "\\") {
		return '"' + "\\\\" + '"'
	}
	if (value.matches(/\(1ULL << \d+\)/)) {
		return value.replaceAll(/(1ULL) << (\d+)/, '1L << $2')
	}
	if (value.matches(/\(1 << \d+\)/)) {
		return value.replaceAll(/(1) << (\d+)/, '1 << $2')
	}
	if (value.startsWith("0x")) {
		return value
	}
	try {
		Eval.me(value)
		return value
	} catch (Exception ignore) {
	}
	try {
		Integer.parseInt(value)
		return value
	} catch (NumberFormatException ignored) {
		return '"' + value + '"'
	}
}

static String findType(String value) {
	if (value == null) {
		return "boolean"
	}
	if (value.matches(/\(1ULL << \d+\)/)) {
		return "long"
	}
	if (value.contains("<<")) {
		return "int"
	}
	if (value.startsWith("0x")) {
		return "int"
	}
	try {
		def eval = Eval.me(value)
		def clazz = eval.class
		if (clazz == Integer.class) {
			return "int"
		}
		if (clazz == String.class) {
			return "String"
		}
		if (clazz == Long.class) {
			return "long"
		}
		return clazz.simpleName
	} catch (Exception ignore) {
	}
	try {
		Integer.parseInt(value)
		return "int"
	} catch (NumberFormatException ignored) {
		return "String"
	}
}

static boolean predefined(constObj, List overrides) {
	overrides.stream()
		.map { it.name }
		.anyMatch { it == constObj.name }
}

tasks.named('sourcesJar') {
	dependsOn tasks.named('callTypesGenerator'),
		tasks.named('channelFunctionGenerator'),
		tasks.named('channelTypeGenerator'),
		tasks.named('constsGenerator'),
		tasks.named('resultCodeGenerator'),
		tasks.named('channelFlagGenerator')
}